!##############################################################################
!
! Ash3d_PostProc_gnuplot module
!
! This module provides the subroutines that use gnuplot for creating 2d maps,
! 2d vertical profiles, and the little deposit accumulation plots linked to
! the airport arrival kml (ash_arrivaltimes_airports.kml).  Although a few
! third party API's are available, these subroutines create temporary script
! files that are run if gnuplot is installed on the local system.
!
!      subroutine write_2Dmap_PNG_gnuplot
!      subroutine write_2Dprof_PNG_gnuplot
!      subroutine write_DepPOI_TS_PNG_gnuplot
!
!##############################################################################

      module Ash3d_PostProc_gnuplot

      use precis_param

      use io_units

      use global_param,  only : &
         DirDelim

      use io_data,       only : &
         Ash3dHome

      implicit none

        ! Set everything to private by default
      private

        ! Publicly available subroutines/functions
      public write_2Dmap_PNG_gnuplot,   &
             write_2Dprof_PNG_gnuplot,  &
             write_DepPOI_TS_PNG_gnuplot

        ! Publicly available variables

!      character(100) :: USGSIconFile

      contains
      !------------------------------------------------------------------------

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_2Dmap_PNG_gnuplot
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    nx            = x length of output array OutVar
!    ny            = y length of output array OutVar
!    iprod         = product ID
!    itime         = time index from netcdf data file
!    OutVar        = 2-d array to be written to ASCII file
!    writeContours = logical
!
!  This subroutine creates a png map of the variable in OutVar using the gnuplot
!  graphics package.  Annotations and contour levels are indicated via the
!  product ID (iprod).  If writeContours is set to true, then this subroutine
!  is only used for generating and storing the contours (for gnuplot, this is
!  through output files generated by the script) with no png written. If timestep = -1,
!  then use the last step in file
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_2Dmap_PNG_gnuplot(nx,ny,iprod,itime,OutVar,writeContours)

      use global_param,  only : &
         EPS_SMALL

      use mesh,          only : &
         x_cc_pd,y_cc_pd,lon_cc_pd,lat_cc_pd, &
         IsLatLon

      use Output_Vars,   only : &
         ContourFilled,Con_Cust,Con_Cust_N,Con_Cust_RGB,Con_Cust_Lev,&
         Con_DepThick_mm_N,Con_DepThick_mm_Lev,Con_DepThick_mm_RGB, &
         Con_DepThick_in_N,Con_DepThick_in_Lev,Con_DepThick_in_RGB, &
         Con_DepTime_N,Con_DepTime_Lev,Con_DepTime_RGB, &
         Con_CloudCon_N,Con_CloudCon_Lev,Con_CloudCon_RGB, &
         Con_CloudTop_N,Con_CloudTop_RGB,Con_CloudTop_Lev, &
         Con_CloudBot_N,Con_CloudBot_RGB,Con_CloudBot_Lev, &
         Con_CloudLoad_N,Con_CloudLoad_RGB,Con_CloudLoad_Lev, &
         Con_CloudRef_N,Con_CloudRef_RGB,Con_CloudRef_Lev, &
         Con_CloudTime_N,Con_CloudTime_RGB,Con_CloudTime_Lev, &
         ContourDataX,ContourDataY,ContourDataNcurves,ContourDataNpoints,&
         Contour_MaxCurves,Contour_MaxPoints,ContourLev,nConLev

      use time_data,     only : &
         os_time_log,SimStartHour,BaseYear,useLeap

      use io_data,       only : &
         WriteTimes,cdf_b3l1,VolcanoName

      use Source,        only : &
         e_Volume,e_Duration,e_StartTime,e_PlumeHeight,lon_volcano,lat_volcano

      use citywriter

      integer      ,intent(in) :: nx
      integer      ,intent(in) :: ny
      integer      ,intent(in) :: iprod
      integer      ,intent(in) :: itime
      real(kind=ip),intent(in) :: OutVar(nx,ny)
      logical      ,intent(in) :: writeContours

      integer :: i,j,ii
      integer     , dimension(:,:),allocatable :: zrgb
      character(len=40) :: title_plot
      character(len=15) :: title_legend
      character(len=40) :: outfile_name
      character(len= 9) :: cio
      character(len= 4) :: outfile_ext = '.png'
      character(len=10) :: units

      real(kind=ip)  :: xmin
      real(kind=ip)  :: xmax
      real(kind=ip)  :: ymin
      real(kind=ip)  :: ymax

      character(len=10) :: dp_gnufile
      character(len=10) :: dp_outfile
      character(len=10) :: dp_confile
      !character(len=26) :: coord_str
      character(len=25) :: gnucom
      character(len=80) :: gnucoastfile
      integer           :: ioerr
      integer           :: iostatus
      character(len=120):: iomessage
      integer           :: iw,iwf

      integer :: ncities
      real(kind=ip),dimension(:),allocatable     :: lon_cities
      real(kind=ip),dimension(:),allocatable     :: lat_cities
      character(len=26),dimension(:),allocatable :: name_cities
      logical           :: IsThere1,IsThere2
      character(len=80) :: linebuffer080
      character         :: testkey
      integer           :: ilev,ignulev
      integer           :: lev_i,substr_pos1,substr_pos2,substr_pos3
      real(kind=4)      :: lev_r4
      integer           :: icurve,ipt

      INTERFACE
        character (len=20) function HS_xmltime(HoursSince,byear,useLeaps)
          real(kind=8)              :: HoursSince
          integer                   :: byear
          logical                   :: useLeaps
        end function HS_xmltime
      END INTERFACE

      write(gnucoastfile,'(a13)')"world_50m.txt"
      inquire(file=gnucoastfile,exist=IsThere1)
      if(.not.IsThere1)then
        gnucoastfile = trim(Ash3dHome) // &
                          DirDelim // 'share' // &
                          DirDelim // 'post_proc' // &
                          DirDelim // 'world_50m.txt'
        inquire(file=gnucoastfile,exist=IsThere2)
        if(.not.IsThere2)then
          do io=1,2;if(VB(io).le.verbosity_error)then
            write(errlog(io),*)"Could not find required file world_50m.txt"
            write(errlog(io),*)"This file is available at:"
            write(errlog(io),*)"  http://www.gnuplotting.org/data/world_50m.txt"
            write(errlog(io),*)"Please download this file to the current working directory or"
#ifdef LINUX
            write(errlog(io),*)"copy to /opt/USGS/Ash3d/share/post_proc/"
#endif
#ifdef MACOS
            write(errlog(io),*)"copy to /opt/USGS/Ash3d/share/post_proc/"
#endif
#ifdef WINDOWS
            write(errlog(io),*)"copy to C:\opt\USGS\Ash3d\share\post_proc\"
#endif
          endif;enddo
          stop 1
        endif
      endif

      ncities = 20
      allocate(lon_cities(ncities))
      allocate(lat_cities(ncities))
      allocate(name_cities(ncities))

      if(iprod.eq.5.or.iprod.eq.6)then
        cio='____final'
      else
        if (WriteTimes(itime).lt.10.0_ip) then
          write(cio,1) WriteTimes(itime)
1         format('00',f4.2,'hrs')
        elseif (WriteTimes(itime).lt.100.0_ip) then
          write(cio,2) WriteTimes(itime)
2         format('0',f5.2,'hrs')
        else
          write(cio,3) WriteTimes(itime)
3         format(f6.2,'hrs')
        endif
      endif

      if(Con_Cust)then
        nConLev = Con_Cust_N
        allocate(zrgb(nConLev,3))
        allocate(ContourLev(nConLev))
        ContourLev(1:nConLev) = Con_Cust_Lev(1:nConLev)
        zrgb(1:nConLev,1:3) = Con_Cust_RGB(1:nConLev,1:3)
      endif

      if(iprod.eq.3)then       ! deposit at specified times (mm)
        write(outfile_name,'(a15,a9,a4)')'Ash3d_Deposit_t',cio,outfile_ext
        write(title_plot,'(a20,f5.2,a6)')'Deposit Thickness t=',WriteTimes(itime),' hours'
        title_legend = 'Dep.Thick.(mm)'
        units = " (mm)"
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_mm_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_mm_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_mm_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.4)then   ! deposit at specified times (inches)
        write(outfile_name,'(a15,a9,a4)')'Ash3d_Deposit_t',cio,outfile_ext
        write(title_plot,'(a20,f5.2,a6)')'Deposit Thickness t=',WriteTimes(itime),' hours'
        title_legend = 'Dep.Thick.(in)'
        units = " (in)"
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_in_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_in_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_in_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.5)then       ! deposit at final time (mm)
        write(outfile_name,'(a13,a9,a4)')'Ash3d_Deposit',cio,outfile_ext
        title_plot = 'Final Deposit Thickness'
        title_legend = 'Dep.Thick.(mm)'
        units = " (mm)"
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_mm_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_mm_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_mm_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.6)then   ! deposit at final time (inches)
        write(outfile_name,'(a13,a9,a4)')'Ash3d_Deposit',cio,outfile_ext
        title_plot = 'Final Deposit Thickness'
        title_legend = 'Dep.Thick.(in)'
        units = " (in)"
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_in_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_in_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_in_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.7)then   ! ashfall arrival time (hours)
        write(outfile_name,'(a22)')'DepositArrivalTime.png'
        write(title_plot,'(a20)')'Ashfall arrival time'
        title_legend = 'Time (hours)'
        units = " (hours)"
        if(.not.Con_Cust)then
          nConLev = Con_DepTime_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepTime_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepTime_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.8)then   ! ashfall arrival at airports/POI (mm)
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: No map PNG output option for airport arrival time data."
          write(errlog(io),*)"       Should not be in write_2Dmap_PNG_dislin"
        endif;enddo
        stop 1
      elseif(iprod.eq.9)then   ! ash-cloud concentration
        write(outfile_name,'(a16,a9,a4)')'Ash3d_CloudCon_t',cio,outfile_ext
        write(title_plot,'(a26,f5.2,a6)')'Ash-cloud concentration t=',WriteTimes(itime),' hours'
        title_legend = 'Max.Con.(mg/m3)'
        units = " (mg/m3)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudCon_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudCon_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudCon_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.10)then   ! ash-cloud height
        write(outfile_name,'(a19,a9,a4)')'Ash3d_CloudHeight_t',cio,outfile_ext
        write(title_plot,'(a19,f5.2,a6)')'Ash-cloud height t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Height(km)'
        units = " (km)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudTop_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudTop_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudTop_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.11)then   ! ash-cloud bottom
        write(outfile_name,'(a16,a9,a4)')'Ash3d_CloudBot_t',cio,outfile_ext
        write(title_plot,'(a19,f5.2,a6)')'Ash-cloud bottom t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Bot.(km)'
        units = " (km)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudBot_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudBot_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudBot_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.12)then   ! ash-cloud load
        write(outfile_name,'(a17,a9,a4)')'Ash3d_CloudLoad_t',cio,outfile_ext
        write(title_plot,'(a17,f5.2,a6)')'Ash-cloud load t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Load(T/km2)'
        units = " (T/km2)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudLoad_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudLoad_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudLoad_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.13)then  ! radar reflectivity
        write(outfile_name,'(a20,a9,a4)')'Ash3d_CloudRadRefl_t',cio,outfile_ext
        write(title_plot,'(a24,f5.2,a6)')'Ash-cloud radar refl. t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Refl.(dBz)'
        units = " (dBz)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudRef_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudRef_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudRef_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.14)then   ! ashcloud arrival time (hours)
        write(outfile_name,'(a20)')'CloudArrivalTime.png'
        write(title_plot,'(a22)')'Ash-cloud arrival time'
        title_legend = 'Time (hours)'
        units = " (hours)"
        if(.not.Con_Cust)then
          nConLev = Con_CloudTime_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudTime_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudTime_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.15)then   ! topography
        write(outfile_name,'(a14)')'Topography.png'
        write(title_plot,'(a10)')'Topography'
        title_legend = 'Elevation (km)'
        units = " (hours)"
        if(.not.Con_Cust)then
          nConLev = 8
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev = (/0.1_ip, 0.3_ip, 1.0_ip, 3.0_ip, &
                  10.0_ip, 30.0_ip, 100.0_ip, 300.0_ip/)
        endif
      elseif(iprod.eq.16)then   ! profile plots
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: No map PNG output option for vertical profile data."
          write(errlog(io),*)"       Should not be in write_2Dmap_PNG_dislin"
        endif;enddo
        stop 1
      else
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: unexpected variable"
        endif;enddo
        stop 1
      endif

      if(writeContours)then
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(outlog(io),*)"Running Gnuplot to calculate contours lines"
        endif;enddo
        write(outfile_name,'(a14)')'tmp.png'
        allocate(ContourDataNcurves(nConLev))
        allocate(ContourDataNpoints(nConLev,Contour_MaxCurves))
        allocate(ContourDataX(nConLev,Contour_MaxCurves,Contour_MaxPoints))
        allocate(ContourDataY(nConLev,Contour_MaxCurves,Contour_MaxPoints))
        ContourDataNcurves(:)   = 0
        ContourDataNpoints(:,:) = 0
        ContourDataX(:,:,:)     = 0.0_ip
        ContourDataY(:,:,:)     = 0.0_ip
      else
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(outlog(io),*)"Running Gnuplot to generate contour plot"
        endif;enddo
      endif

      write(dp_outfile,53) "outvar.dat"
      write(dp_confile,53) "outvar.con"
      write(dp_gnufile,53) "outvar.gpi"
 53   format(a10)

      if(IsLatLon)then
        xmin = minval(lon_cc_pd(1:nx))
        ! Make sure xmin is in the range -180->180
        if (xmin.gt.180.0_ip)then
          xmin = minval(lon_cc_pd(1:nx))-360.0_ip
          xmax = maxval(lon_cc_pd(1:nx))-360.0_ip
        else
          xmax = maxval(lon_cc_pd(1:nx))
        endif
        ymin = minval(lat_cc_pd(1:ny))
        ymax = maxval(lat_cc_pd(1:ny))
      else
        xmin = minval(x_cc_pd(1:nx))
        xmax = maxval(x_cc_pd(1:nx))
        ymin = minval(y_cc_pd(1:ny))
        ymax = maxval(y_cc_pd(1:ny))
      endif
      call citylist(2,xmin,xmax,ymin,ymax, &
                      ncities,                        &
                      lon_cities,lat_cities,          &
                      name_cities)

      if(lon_volcano.gt.xmax)lon_volcano=lon_volcano-360.0_ip

      ! write out the data in a form that gnuplot can read
      open(54,file=dp_outfile,status='replace')
      do i = 1,nx
        do j = 1,ny
          if(lon_cc_pd(1).lt.180.0_ip)then
            write(54,*)lon_cc_pd(i),lat_cc_pd(j),OutVar(i,j)
          else
            write(54,*)lon_cc_pd(i)-360.0_ip,lat_cc_pd(j),OutVar(i,j)
          endif
        enddo
        write(54,*)""
      enddo
      close(54)
      open(54,file="volc.dat",status='replace')
      write(54,*)real(lon_volcano,kind=4),real(lat_volcano,kind=4),'""'
      close(54)

      ! Set up to plot via gnuplot script
      open(55,file=dp_gnufile,status='replace')
      write(55,*)"set terminal pngcairo font 'sans,12' size 854,603"   ! Set the image size
      write(55,*)"set origin 0.05, .20"
      write(55,*)"set size 0.85, 0.8"              ! Set x and y scale for plot
      write(55,*)"set ylabel 'Latitude'"
      write(55,*)"set xlabel 'Longitude'"
      write(55,*)"set output '",trim(adjustl(outfile_name)),"'"
      write(55,*)"set title '",trim(adjustl(title_plot)),units,"'"
      write(55,*)"XMIN = ",real(xmin,kind=4)
      write(55,*)"YMIN = ",real(ymin,kind=4)
      write(55,*)"XMAX = ",real(xmax,kind=4)
      write(55,*)"YMAX = ",real(ymax,kind=4)

      write(55,*)"set xrange [XMIN:XMAX]"
      write(55,*)"set yrange [YMIN:YMAX]"
      write(55,*)"set contour base"
      write(55,*)"set cntrparam bspline"
      write(55,*)"set cntrparam levels discrete \"
      do i=1,nConLev-1
        write(55,*)real(ContourLev(i),kind=4),', \'
      enddo
      write(55,*)real(ContourLev(nConLev),kind=4)
      write(55,*)"unset surface"
      ! Now write out the contours to a datafile
      write(55,*)"set table 'outvar.con'"
      write(55,*)"splot 'outvar.dat' using 1:2:3"
      write(55,*)"unset table"
      write(55,*)"set style line 2 lc rgb '#808080' lt 0 lw 1"
      write(55,*)"set grid front ls 2"
      write(55,*)"unset key"

      write(55,*)"XVAL = XMIN-(XMAX-XMIN)*0.1"
      write(55,*)"YVAL = YMIN-(YMAX-YMIN)*0.25"

      write(55,*)"set label 'Volcano: " ,VolcanoName,&
                  "' at XVAL, YVAL font 'sans,9'"
      write(55,*)"set label 'Run Date: ",os_time_log,&
                  "' at XVAL, YVAL font 'sans,9' offset character 0,-1"
      read(cdf_b3l1,*,iostat=iostatus,iomsg=iomessage) iw,iwf
      if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,cdf_b3l1,iomessage)
      write(55,*)"set label 'Windfile: ",iwf,&
                  "' at XVAL, YVAL font 'sans,9' offset character 0,-2"

      write(55,*)"XVAL = XMIN+(XMAX-XMIN)*0.4"
      write(55,*)"set label 'Erup. Start Time: ",HS_xmltime(SimStartHour+e_StartTime(1),BaseYear,useLeap),&
                  "' at XVAL, YVAL font 'sans,9'"
      write(55,*)"set label 'Erup. Plume Height: ",real(e_PlumeHeight(1),kind=4),&
                  " km' at XVAL, YVAL font 'sans,9' offset character 0,-1"
      write(55,*)"set label 'Erup. Duration: ",real(e_Duration(1),kind=4),&
                  " hours' at XVAL, YVAL font 'sans,9' offset character 0,-2"
      write(55,*)"set label 'Erup. Volume: ",real(e_Volume(1),kind=4),&
                  " km3 (DRE)' at XVAL, YVAL font 'sans,9' offset character 0,-3"

      write(55,*)" plot '",trim(adjustl(gnucoastfile)),"' with filledcurves linetype rgb '#dddddd' , \"
      write(55,*)"   'outvar.con' using 1:2 with l lc rgb '#888888' , \"
      write(55,*)"   '' every 1000 with labels font ',6' , \"
      write(55,*)"   'cities.xy' using 1:2 , \"
      write(55,*)"   '' using 1:2:3 with labels font ',10' point pointtype 7 offset char 1,1, \"
      write(55,*)"   'volc.dat' using 1:2 , \"
      write(55,*)"   '' using 1:2:3 with labels point pointtype 22 pointsize 2 lt rgb 'red'"

      close(55)

      write(gnucom,'(a11,a14)')'gnuplot -p ',dp_gnufile
      call execute_command_line(gnucom,exitstat=iostatus)

      if(writeContours)then

        ! Read outvar.con
        do io=1,2;if(VB(io).le.verbosity_info)then
          write(outlog(io),*)"Now reading outvar.con and loading contour data."
        endif;enddo
        open(54,file=dp_confile,status='old')
        ! In the gnuplot contour file, all contours of a certain level have a header
        ! in the following format:
        !# Contour 0, label:      300
        ! Each curve for that level is separated by a blank line
        ! Gnuplot writes the contour data to file starting with the highest level
        ! so we will need to check with zlev(:) to make sure we populate ContourDat
        ! correctly.
        ilev = -1
        ignulev = -1
        read(54,'(a80)',iostat=iostatus,iomsg=iomessage)linebuffer080
        if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
        do while(iostatus.eq.0)
          ! Check if this is a header line
          read(linebuffer080,*,iostat=ioerr,iomsg=iomessage)testkey
          if(ioerr.lt.0)then
            ! if there was an error trying to read a character, then this is a blank
            ! line; check if we are in a contour block or still in the file header
            if(ignulev.eq.-1)then
              ! Still in file header
              ! Read the next line and cycle
              read(54,'(a80)',iostat=iostatus,iomsg=iomessage)linebuffer080
              !if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
              cycle
            else
              ! Blank line in a contour block means we are starting another curve
              ! Increment the number of curves for this level
              ContourDataNcurves(ilev) = ContourDataNcurves(ilev) + 1
              ! This is an easier index to used
              icurve = ContourDataNcurves(ilev)
            endif
          elseif(testkey.eq.'#')then
            ! This is a header line
            ! There are two possibilities:
            !  (1) a line of the file header
            !  (2) the start of a contour block
            !   if (2), then it will have this format:# Contour 0, label:      300
            substr_pos1 = index(linebuffer080,'Contour')

            if(substr_pos1.eq.0)then
              ! This is a file header line
              read(54,'(a80)',iostat=iostatus,iomsg=iomessage)linebuffer080
              cycle
            else
              read(linebuffer080,4,iostat=ioerr,iomsg=iomessage)ignulev
              if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
4             format(9x,i2)
              ! Now read the level. Look for the ':' to isolate the last bit
              substr_pos1 = index(linebuffer080,':')
              ! Also look for a '.' or an 'e' to see if the level value is written as a real or int
              substr_pos2 = index(linebuffer080,'.')
              substr_pos3 = index(linebuffer080(20:),'e')
              if(substr_pos2.gt.0.or.substr_pos3.gt.0)then
                ! level is written as real
                read(linebuffer080(substr_pos1+1:28),*,iostat=iostatus,iomsg=iomessage)lev_r4
                if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
              else
                read(linebuffer080(substr_pos1+1:28),*,iostat=iostatus,iomsg=iomessage)lev_i
                if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
                lev_r4 = real(lev_i,kind=4)
              endif
              ! The value for this new level ignulev is lev_r4, but we need to find which
              ! ContourLev this corresponds to
              do ii = 1,nConLev
                if(abs(lev_r4-real(ContourLev(ii),kind=4)).lt.EPS_SMALL)then
                  ilev=ii
                endif
              enddo
              ! When we have a new level, initialize the curve index for this level to 1
              ContourDataNcurves(ilev) = 1
              icurve = ContourDataNcurves(ilev)
            endif
          else
            ! This is the data section
            ! Increment the number of points
            ContourDataNpoints(ilev,icurve) = ContourDataNpoints(ilev,icurve) + 1
            ipt = ContourDataNpoints(ilev,icurve) 
            read(linebuffer080,*,iostat=iostatus,iomsg=iomessage) &
                       ContourDataX(ilev,icurve,ipt),ContourDataY(ilev,icurve,ipt)
            if(iostatus.ne.0) call FileIO_Error_Handler(iostatus,linebuffer080,iomessage)
          endif

          ! Try to read the next line
          read(54,'(a80)',iostat=iostatus,iomsg=iomessage)linebuffer080
        enddo
        close(54)

        ! Loop through all the levels and curves and trim any curves with zero length
        do i=1,nConLev
          icurve = Contour_MaxCurves + 1
          do ii=Contour_MaxCurves,1,-1
            if(ContourDataNpoints(i,ii).le.0)then
              ! log each curve number with no points
              icurve = ii
            endif
          enddo
          ContourDataNcurves(i) = max(0,icurve-1)
        enddo

      endif

      ! clean up memory
      if(allocated(lon_cities))         deallocate(lon_cities)
      if(allocated(lat_cities))         deallocate(lat_cities)
      if(allocated(name_cities))        deallocate(name_cities)
      if(allocated(zrgb))               deallocate(zrgb)

      end subroutine write_2Dmap_PNG_gnuplot

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_2Dprof_PNG_gnuplot
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    vprof_ID        = ID of the profile (number in list from Ash3d control file)
!
!  This subroutine creates a png plot of the transient vertical profile of ash
!  concentration above the profile point (vprof_ID) using the gnuplot graphics
!  package.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_2Dprof_PNG_gnuplot(vprof_ID)

      use global_param,  only : &
         KG_2_MG,KM3_2_M3

      use mesh,          only : &
         nzmax,z_cc_pd

      use Output_Vars,   only : &
         pr_ash,CLOUDCON_THRESH

      use io_data,       only : &
         Site_vprofile,x_vprofile,y_vprofile,cdf_b3l1,VolcanoName

      use Source,        only : &
         e_Volume,e_Duration,e_StartTime,e_PlumeHeight

      use time_data,     only : &
         os_time_log,SimStartHour,BaseYear,useLeap,ntmax,time_native

      integer, intent (in) :: vprof_ID

      character(len=14) :: dp_gnufile
      character(len=14) :: dp_outfile
      character(len=14) :: dp_pngfile
      character(len=26) :: coord_str
      character(len=25) :: gnucom
      integer :: k,i
      integer :: ioerr,iw,iwf

      real(kind=ip)  :: tmin
      real(kind=ip)  :: tmax
      real(kind=ip)  :: zmin
      real(kind=ip)  :: zmax
      real(kind=ip)  :: cloudcon_thresh_mgm3
      real(kind=ip)  :: cmin
      real(kind=ip)  :: cmax

      INTERFACE
        character (len=20) function HS_xmltime(HoursSince,byear,useLeaps)
          real(kind=8)              :: HoursSince
          integer                   :: byear
          logical                   :: useLeaps
        end function HS_xmltime
      END INTERFACE

      write(dp_outfile,53) vprof_ID,".dat"
      write(dp_gnufile,53) vprof_ID,".gpi"
      write(dp_pngfile,54) vprof_ID,".png"
 53   format('vprof_',i4.4,a4)
 54   format('gnupl_',i4.4,a4)

      cloudcon_thresh_mgm3 = CLOUDCON_THRESH * KG_2_MG / KM3_2_M3 !convert from kg/km3 to mg/m3

      tmin=real(0.0,kind=ip)
      tmax=real(ceiling(time_native(ntmax)),kind=ip)
      zmin=real(0.0,kind=ip)
      zmax=real(z_cc_pd(nzmax),kind=ip)
      cmin=real(0.0,kind=ip)
      cmax=real(maxval(pr_ash(:,:,vprof_ID)),kind=ip)       ! Get the max value for this profile
      cmax=real(max(cmax,cloudcon_thresh_mgm3),kind=ip)  ! Do not let cmax drop below the threshold

      open(54,file=dp_outfile,status='replace')
      do i = 1,ntmax
        do k = 1,nzmax
          write(54,*)time_native(i),z_cc_pd(k),pr_ash(k,i,vprof_ID)
        enddo
        write(54,*)""
      enddo
      close(54)

      write(coord_str,101)x_vprofile(vprof_ID),y_vprofile(vprof_ID)
 101  format(' (lon=',f7.2,', lat=',f6.2,')')
      ! Set up to plot via gnuplot script
      open(55,file=dp_gnufile,status='replace')
      write(55,*)"set terminal pngcairo font 'sans,12' size 854,603"   ! Set the image size
      write(55,*)"set origin 0, .10"
      write(55,*)"set size 0.85, 0.9"              ! Set x and y scale for plot
      write(55,*)"set ylabel 'Height (km)'"
      write(55,*)"set xlabel 'Time (hours after eruption)'"
      write(55,*)"set output '",dp_pngfile,"'"
      write(55,*)"set title '",&
                  trim(adjustl(Site_vprofile(vprof_ID))),&
                  coord_str,"'"
      write(55,*)"set isosamples 50"
      write(55,*)"set pm3d"
      write(55,*)"set palette cubehelix negative"
      write(55,*)"unset surface"
      write(55,*)"set view map"
      write(55,*)"set key off"

      write(55,*)"XMIN = 0.0"
      write(55,*)"YMIN = 0.0"
      write(55,*)"XMAX = ",time_native(ntmax)
      write(55,*)"YMAX = ",z_cc_pd(nzmax)
      write(55,*)"XVAL = -XMAX*0.1"
      write(55,*)"YVAL = -YMAX*0.25"
      
      write(55,*)"set label 'Volcano: " ,VolcanoName,&
                  "' at XVAL, YVAL font 'sans,9'"
      write(55,*)"set label 'Run Date: ",os_time_log,&
                  "' at XVAL, YVAL font 'sans,9' offset character 0,-1"
      read(cdf_b3l1,*,iostat=ioerr) iw,iwf
      write(55,*)"set label 'Windfile: ",iwf,&
                  "' at XVAL, YVAL font 'sans,9' offset character 0,-2"

      write(55,*)"XVAL = XMAX*0.4"
      write(55,*)"set label 'Erup. Start Time: ",HS_xmltime(SimStartHour+e_StartTime(1),BaseYear,useLeap),&
                  "' at XVAL, YVAL font 'sans,9'"
      write(55,*)"set label 'Erup. Plume Height: ",real(e_PlumeHeight(1),kind=4),&
                  " km' at XVAL, YVAL font 'sans,9' offset character 0,-1"
      write(55,*)"set label 'Erup. Duration: ",real(e_Duration(1),kind=4),&
                  " hours' at XVAL, YVAL font 'sans,9' offset character 0,-2"
      write(55,*)"set label 'Erup. Volume: ",real(e_Volume(1),kind=4),&
                  " km3 (DRE)' at XVAL, YVAL font 'sans,9' offset character 0,-3"

      write(55,*)"set cblabel 'Ash con. in mg/m3'"
      write(55,*)"splot '",dp_outfile,"'"

      close(55)

      write(gnucom,'(a11,a14)')'gnuplot -p ',dp_gnufile
      call execute_command_line(gnucom)

      end subroutine write_2Dprof_PNG_gnuplot

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_DepPOI_TS_PNG_gnuplot
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    pt_indx       = index of point in Ash3d netcdf output file
!
!  This subroutine creates a png plot of the transient deposit accumulation at
!  the airport/POI given by pt_index using the gnuplot graphics package.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_DepPOI_TS_PNG_gnuplot(pt_indx)

      use Output_Vars,   only : &
         THICKNESS_THRESH

      use Airports,      only : &
         Airport_Name,Airport_Thickness_TS

      use io_data,       only : &
         nWriteTimes,WriteTimes

      use time_data,     only : &
         Simtime_in_hours

      integer :: pt_indx,i

      real(kind=dp) :: ymaxpl
      character(len=14) :: dp_gnufile
      character(len=14) :: dp_outfile
      character(len=14) :: dp_pngfile
      character(len=25) :: gnucom
      integer,save      :: plot_index = 0

      if(Airport_Thickness_TS(pt_indx,nWriteTimes).lt.THICKNESS_THRESH)then
        return
      else
        plot_index = plot_index + 1
      endif

      write(dp_outfile,53) plot_index,".dat"
      write(dp_gnufile,53) plot_index,".gpi"
      write(dp_pngfile,54) plot_index,".png"
 53   format('depTS_',i4.4,a4)
 54   format('gnupl_',i4.4,a4)

      open(54,file=dp_outfile,status='replace')
      do i = 1,nWriteTimes
        write(54,*)WriteTimes(i),Airport_Thickness_TS(pt_indx,i)
      enddo
      close(54)

      if(Airport_Thickness_TS(plot_index,nWriteTimes).lt.THICKNESS_THRESH)then
        ymaxpl = 1.0_dp
      elseif(Airport_Thickness_TS(plot_index,nWriteTimes).lt.1.0_dp)then
        ymaxpl = 1.0_dp
      elseif(Airport_Thickness_TS(plot_index,nWriteTimes).lt.5.0_dp)then
        ymaxpl = 5.0_dp
      elseif(Airport_Thickness_TS(plot_index,nWriteTimes).lt.25.0_dp)then
        ymaxpl = 25.0_dp
      else
        ymaxpl = 100.0_dp
      endif

      ! Set up to plot via gnuplot script
      open(55,file=dp_gnufile,status='replace')
      write(55,*)"set terminal png size 400,300"
      write(55,*)"set key bmargin left horizontal Right noreverse enhanced ",&
                 "autotitles box linetype -1 linewidth 1.000"
      write(55,*)"set border 31 lw 2.0 lc rgb '#000000'"
      write(55,*)"set style line 1 linecolor rgbcolor '#888888' linewidth 2.0 pt 7"
      write(55,*)"set ylabel 'Deposit Thickeness (mm)'"
      write(55,*)"set xlabel 'Time (hours after eruption)'"
      write(55,*)"set nokey"
      write(55,*)"set output '",dp_pngfile,"'"
      write(55,*)"set title '",Airport_Name(pt_indx),"'"
      write(55,*)"plot [0:",ceiling(Simtime_in_hours),"][0:",&
                 nint(ymaxpl),"] '",dp_outfile,"' with filledcurve x1 ls 1"
      close(55)

      write(gnucom,'(a11,a14)')'gnuplot -p ',dp_gnufile
      call execute_command_line(gnucom)

      end subroutine write_DepPOI_TS_PNG_gnuplot

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      end module Ash3d_PostProc_gnuplot

!##############################################################################
