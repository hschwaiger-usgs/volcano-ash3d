!##############################################################################
!
! Ash3d_PostProc_GMT module
!
! This module provides the subroutines that use GMT for creating 2d maps,
! 2d vertical profiles, and the little deposit accumulation plots linked to
! the airport arrival kml (ash_arrivaltimes_airports.kml). Although a
! third party API is available, these subroutines create temporary script
! files that are run if GMT is installed on the local system.
!
!      subroutine write_2Dmap_PNG_GMT
!      subroutine write_2Dprof_PNG_GMT
!      subroutine write_DepPOI_TS_PNG_GMT
!
!##############################################################################

      module Ash3d_PostProc_GMT

      use precis_param

      use io_units

      use global_param,  only : &
         DirDelim

      use io_data,       only : &
         Ash3dHome,concenfile

#if USEGMT
      ! This is only for the experimental GMT API
      use gmt
#endif

      implicit none

        ! Set everything to private by default
      private

        ! Publicly available subroutines/functions
      public write_2Dmap_PNG_GMT,  &
             write_2Dprof_PNG_GMT, &
             write_DepPOI_TS_PNG_GMT

        ! Publicly available variables

      character(100) :: USGSIconFile

      contains
      !------------------------------------------------------------------------

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_2Dmap_PNG_GMT
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    nx            = x length of output array OutVar
!    ny            = y length of output array OutVar
!    iprod         = product ID
!    itime         = time index from netcdf data file
!    OutVar        = 2-d array to be written to ASCII file
!    writeContours = logical
!
!  This subroutine creates a png map of the variable in OutVar using the GMT
!  graphics package.  Annotations and contour levels are indicated via the
!  product ID (iprod).  If writeContours is set to true, then this subroutine
!  is only used for generating and storing the contours (for GMT, this is
!  through output files generated by the script) with no png written. If timestep = -1,
!  then use the last step in file
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_2Dmap_PNG_GMT(nx,ny,iprod,itime,OutVar,writeContours)

      use mesh,          only : &
         x_cc_pd,y_cc_pd,lon_cc_pd,lat_cc_pd, &
         IsLatLon

      use Output_Vars,   only : &
         ContourFilled,Con_Cust,Con_Cust_N,Con_Cust_RGB,Con_Cust_Lev,&
         Con_DepThick_mm_N,Con_DepThick_mm_Lev,Con_DepThick_mm_RGB, &
         Con_DepThick_in_N,Con_DepThick_in_Lev,Con_DepThick_in_RGB, &
         Con_DepTime_N,Con_DepTime_Lev,Con_DepTime_RGB, &
         Con_CloudCon_N,Con_CloudCon_Lev,Con_CloudCon_RGB, &
         Con_CloudTop_N,Con_CloudTop_RGB,Con_CloudTop_Lev, &
         Con_CloudBot_N,Con_CloudBot_RGB,Con_CloudBot_Lev, &
         Con_CloudLoad_N,Con_CloudLoad_RGB,Con_CloudLoad_Lev, &
         Con_CloudRef_N,Con_CloudRef_RGB,Con_CloudRef_Lev, &
         Con_CloudTime_N,Con_CloudTime_RGB,Con_CloudTime_Lev, &
         ContourDataX,ContourDataY,ContourDataNcurves,ContourDataNpoints,&
         Contour_MaxCurves,Contour_MaxPoints,ContourLev,nConLev,Mask_Cloud,&
         CloudArrivalTime,Mask_Deposit

      use time_data,     only : &
         os_time_log,SimStartHour,BaseYear,useLeap

      use io_data,       only : &
         WriteTimes,cdf_b3l1,VolcanoName

      use Source,        only : &
         e_Volume,e_Duration,e_StartTime,e_PlumeHeight,lon_volcano,lat_volcano

      use Ash3d_ASCII_IO,  only : &
           write_2D_ASCII

      use citywriter

      integer      ,intent(in) :: nx
      integer      ,intent(in) :: ny
      integer      ,intent(in) :: iprod
      integer      ,intent(in) :: itime
      real(kind=ip),intent(in) :: OutVar(nx,ny)
      logical      ,intent(in) :: writeContours

      logical            :: mask(nx,ny)
      character(len=6)   :: Fill_Value
      character(len=16)  :: filedat
      character(len=16)  :: filetxt
      character(len=16)  :: fileps  = "temp.ps"
      character(len=200) :: cmd

      integer :: i,j,ii
      integer     , dimension(:,:),allocatable :: zrgb
      character(len=40) :: title_plot
      character(len=15) :: title_legend
      character(len=40) :: outfile_name
      character(len= 9) :: cio
      character(len= 4) :: outfile_ext = '.png'
      character(len=10) :: units

      real(kind=ip)  :: xmin
      real(kind=ip)  :: xmax
      real(kind=ip)  :: ymin
      real(kind=ip)  :: ymax

      character(len=10) :: dp_gmtfile
      character(len=10) :: dp_outfile
      character(len=10) :: dp_confile
      character(len=25) :: gmtcom
      integer :: ioerr,ioerr2,iw,iwf,istat

      integer :: ncities
      real(kind=ip),dimension(:),allocatable     :: lon_cities
      real(kind=ip),dimension(:),allocatable     :: lat_cities
      character(len=26),dimension(:),allocatable :: name_cities
      logical           :: IsThere1,IsThere2
      character(len=80) :: linebuffer080
      character         :: testkey
      integer           :: ilev,ignulev
      integer           :: lev_i,substr_pos1,substr_pos2,substr_pos3
      real(kind=4)      :: lev_r4
      integer           :: icurve,ipt

      character(len=20) :: varname
      character(len=20),dimension(20) :: penstr
      character(len=80),dimension(20) :: legpenstr
      character(len=17) :: dumstr17
      character(len=48) :: dumstr48
      character(len=8)  :: flt_str
      character(len=50) :: base_str
      character(len=50) :: title_str
      character(len=4 ) :: detail_str
      character(len=50) :: proj_str
      character(len=50) :: area_str
      character(len=50) :: coast_str
      character(len=50) :: river_str
      character(len=50) :: start_ps
      character(len=50) :: contn_ps
      character(len=50) :: end_ps
      logical  :: IsThere

      INTERFACE
        character (len=20) function HS_xmltime(HoursSince,byear,useLeaps)
          real(kind=8)              :: HoursSince
          integer                   :: byear
          logical                   :: useLeaps
        end function HS_xmltime
      END INTERFACE

      ncities = 20
      allocate(lon_cities(ncities))
      allocate(lat_cities(ncities))
      allocate(name_cities(ncities))

      if(iprod.eq.5.or.iprod.eq.6)then
        cio='____final'
      else
        if (WriteTimes(itime).lt.10.0_ip) then
          write(cio,1) WriteTimes(itime)
1         format('00',f4.2,'hrs')
        elseif (WriteTimes(itime).lt.100.0_ip) then
          write(cio,2) WriteTimes(itime)
2         format('0',f5.2,'hrs')
        else
          write(cio,3) WriteTimes(itime)
3         format(f6.2,'hrs')
        endif
      endif

      if(Con_Cust)then
        nConLev = Con_Cust_N
        allocate(zrgb(nConLev,3))
        allocate(ContourLev(nConLev))
        ContourLev(1:nConLev) = Con_Cust_Lev(1:nConLev)
        zrgb(1:nConLev,1:3) = Con_Cust_RGB(1:nConLev,1:3)
      endif

      if(iprod.eq.3)then       ! deposit at specified times (mm)
        varname = "depothick"
        write(outfile_name,'(a15,a9,a4)')'Ash3d_Deposit_t',cio,outfile_ext
        write(title_plot,'(a20,f5.2,a6)')'Deposit Thickness t=',WriteTimes(itime),' hours'
        title_legend = 'Dep.Thick.(mm)'
        units = " (mm)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_mm_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_mm_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_mm_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.4)then   ! deposit at specified times (inches)
        varname = "depothick"
        write(outfile_name,'(a15,a9,a4)')'Ash3d_Deposit_t',cio,outfile_ext
        write(title_plot,'(a20,f5.2,a6)')'Deposit Thickness t=',WriteTimes(itime),' hours'
        title_legend = 'Dep.Thick.(in)'
        units = " (in)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_in_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_in_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_in_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.5)then       ! deposit at final time (mm)
        varname = "depothickFin"
        write(outfile_name,'(a13,a9,a4)')'Ash3d_Deposit',cio,outfile_ext
        title_plot = 'Final Deposit Thickness'
        title_legend = 'Dep.Thick.(mm)'
        units = " (mm)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_mm_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_mm_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_mm_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.6)then   ! deposit at final time (inches)
        varname = "depothickFin"
        write(outfile_name,'(a13,a9,a4)')'Ash3d_Deposit',cio,outfile_ext
        title_plot = 'Final Deposit Thickness'
        title_legend = 'Dep.Thick.(in)'
        units = " (in)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_DepThick_in_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepThick_in_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepThick_in_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.7)then   ! ashfall arrival time (hours)
        varname = "depotime"
        write(outfile_name,'(a22)')'DepositArrivalTime.png'
        write(title_plot,'(a20)')'Ashfall arrival time'
        title_legend = 'Time (hours)'
        units = " (hours)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_DepTime_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_DepTime_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_DepTime_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.8)then   ! ashfall arrival at airports/POI (mm)
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: No map PNG output option for airport arrival time data."
          write(errlog(io),*)"       Should not be in write_2Dmap_PNG_dislin"
        endif;enddo
        stop 1
      elseif(iprod.eq.9)then   ! ash-cloud concentration
        varname = "ashcon_max"
        write(outfile_name,'(a16,a9,a4)')'Ash3d_CloudCon_t',cio,outfile_ext
        write(title_plot,'(a26,f5.2,a6)')'Ash-cloud concentration t=',WriteTimes(itime),' hours'
        title_legend = 'Max.Con.(mg/m3)'
        units = " (mg/m3)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudCon_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudCon_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudCon_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.10)then   ! ash-cloud height
        varname = "cloud_height"
        write(outfile_name,'(a19,a9,a4)')'Ash3d_CloudHeight_t',cio,outfile_ext
        write(title_plot,'(a19,f5.2,a6)')'Ash-cloud height t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Height(km)'
        units = " (km)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudTop_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudTop_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudTop_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.11)then   ! ash-cloud bottom
        varname = "cloud_bottom"
        write(outfile_name,'(a16,a9,a4)')'Ash3d_CloudBot_t',cio,outfile_ext
        write(title_plot,'(a19,f5.2,a6)')'Ash-cloud bottom t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Bot.(km)'
        units = " (km)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudBot_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudBot_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudBot_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.12)then   ! ash-cloud load
        varname = "cloud_load"
        write(outfile_name,'(a17,a9,a4)')'Ash3d_CloudLoad_t',cio,outfile_ext
        write(title_plot,'(a17,f5.2,a6)')'Ash-cloud load t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Load(T/km2)'
        units = " (T/km2)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudLoad_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudLoad_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudLoad_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.13)then  ! radar reflectivity
        varname = "radar_reflectivity"
        write(outfile_name,'(a20,a9,a4)')'Ash3d_CloudRadRefl_t',cio,outfile_ext
        write(title_plot,'(a24,f5.2,a6)')'Ash-cloud radar refl. t=',WriteTimes(itime),' hours'
        title_legend = 'Cld.Refl.(dBz)'
        units = " (dBz)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudRef_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudRef_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudRef_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.14)then   ! ashcloud arrival time (hours)
        varname = "ash_arrival_time"
        write(outfile_name,'(a20)')'CloudArrivalTime.png'
        write(title_plot,'(a22)')'Ash-cloud arrival time'
        title_legend = 'Time (hours)'
        units = " (hours)"
        Fill_Value = '-9999.'
        if(.not.Con_Cust)then
          nConLev = Con_CloudTime_N
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev(1:nConLev) = Con_CloudTime_Lev(1:nConLev)
          zrgb(1:nConLev,1:3) = Con_CloudTime_RGB(1:nConLev,1:3)
        endif
      elseif(iprod.eq.15)then   ! topography
        varname = "topography"
        write(outfile_name,'(a14)')'Topography.png'
        write(title_plot,'(a10)')'Topography'
        title_legend = 'Elevation (km)'
        units = " (hours)"
        if(.not.Con_Cust)then
          nConLev = 8
          allocate(zrgb(nConLev,3))
          allocate(ContourLev(nConLev))
          ContourLev = (/0.1_ip, 0.3_ip, 1.0_ip, 3.0_ip, &
                  10.0_ip, 30.0_ip, 100.0_ip, 300.0_ip/)
        endif
      elseif(iprod.eq.16)then   ! profile plots
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: No map PNG output option for vertical profile data."
          write(errlog(io),*)"       Should not be in write_2Dmap_PNG_dislin"
        endif;enddo
        stop 1
      else
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(errlog(io),*)"ERROR: unexpected variable"
        endif;enddo
        stop 1
      endif

      ! When we run this subroutine via a GMT script (as opposed to the API), we
      ! write out OutVar to an ESRI ASCII file to be read by the script
      ! Now mask out non-cloud values
      if(iprod.eq.10.or.&  ! CloudHeight
         iprod.eq.11)then  ! CloudHeightBot
        mask = Mask_Cloud
      elseif(iprod.eq.7)then ! DepArrivalTime
        mask(1:nx,1:ny) = Mask_Deposit(1:nx,1:ny)
      elseif(iprod.eq.14)then
        ! cloud mask based on cloud load does not work in this case the cloud load mask
        ! is a function of time
        mask(1:nx,1:ny) = .true.
        do i=1,nx
          do j=1,ny
            if(CloudArrivalTime(i,j).lt.0.0_ip)mask(i,j) = .false.
          enddo
        enddo
      else
        mask = .true.
      endif

      varname = "outvar"
      call write_2D_ASCII(nx,ny,OutVar,mask,Fill_Value,varname)

      ! write contour pen specifications to strings
      do i=1,nConLev
        write(flt_str,'(i3)')zrgb(i,1)
        penstr(i)="-W3," // adjustl(trim(flt_str))
        legpenstr(i)="S 0.1i - 0.15i black 3.0p," // adjustl(trim(flt_str))
        write(flt_str,'(i3)')zrgb(i,2)
        penstr(i)= trim(penstr(i)) // "/" // adjustl(trim(flt_str))
        legpenstr(i)=trim(legpenstr(i)) // "/" // adjustl(trim(flt_str))
        write(flt_str,'(i3)')zrgb(i,3)
        penstr(i)= trim(penstr(i)) // "/" // adjustl(trim(flt_str))
        legpenstr(i)=trim(legpenstr(i)) // "/" // adjustl(trim(flt_str))
        write(flt_str,'(g8.3)')ContourLev(i)
        legpenstr(i)=trim(legpenstr(i)) // " 0.3i " // adjustl(trim(flt_str))
      enddo

      if(writeContours)then
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(outlog(io),*)"Running GMT to calculate contours lines"
        endif;enddo
        write(outfile_name,'(a14)')'tmp.png'
        allocate(ContourDataNcurves(nConLev))
        allocate(ContourDataNpoints(nConLev,Contour_MaxCurves))
        allocate(ContourDataX(nConLev,Contour_MaxCurves,Contour_MaxPoints))
        allocate(ContourDataY(nConLev,Contour_MaxCurves,Contour_MaxPoints))
        ContourDataNcurves(:)   = 0
        ContourDataNpoints(:,:) = 0
        ContourDataX(:,:,:)     = 0.0_ip
        ContourDataY(:,:,:)     = 0.0_ip
      else
        do io=1,2;if(VB(io).le.verbosity_error)then
          write(outlog(io),*)"Running GMT to generate contour plot"
        endif;enddo
      endif

      write(dp_confile,53) "outvar.con"
      write(dp_gmtfile,53) "outvar.gmt"
 53   format(a10)

      ! Set up to plot via GMT script
      open(55,file=dp_gmtfile,status='replace')

      ! Write annotations to several legends
      !  Left panel
      open(61,file="leg1.txt",status='replace')

      write(61,'(a1)')"P"
      write(61,'(a11,a30)')"T Volcano: ",VolcanoName
      write(61,'(a5)')"G0.2i"
      write(61,'(a12,a20)')"T Run Date: ",os_time_log
      write(61,'(a5)')"G0.2i"
      read(cdf_b3l1,*,iostat=ioerr) iw,iwf
      write(61,'(a12,i3)')"T Windfile: ",iwf
      close(61)
      !  Right panel
      open(62,file="leg2.txt",status='replace')
      write(62,'(a20,a20)')"T Erup. Start Time: ",HS_xmltime(SimStartHour+e_StartTime(1),BaseYear,useLeap)
      write(62,'(a5)')"G0.2i"
      write(62,'(a22,f5.2,a3)')"T Erup. Plume Height: ",real(e_PlumeHeight(1),kind=4)," km"
      write(62,'(a)')"G0.2i"
      write(62,'(a18,f5.2,a6)')"T Erup. Duration: ",real(e_Duration(1),kind=4)," hours"
      write(62,'(a5)')"G0.2i"
      write(62,'(a16,f5.2,a9)')"T Erup. Volume: ",real(e_Volume(1),kind=4)," km3(DRE)"
      close(62)

      !  USGS Logo
      USGSIconFile = trim(Ash3dHome) // &
                        DirDelim // 'share' // &
                        DirDelim // 'post_proc' // &
                        DirDelim // 'USGSvid.png'
      inquire( file=trim(adjustl(USGSIconFile)), exist=IsThere)
      if(IsThere)then
        open(63,file="leg3.txt",status='replace')
        write(63,'(g0)')"I " // adjustl(trim(USGSIconFile)) // " 2i C"
        close(63)
      endif

      !  Contour legend
      open(64,file="leg4.txt",status='replace')
        write(64,'(a7)')"C black"
        write(64,'(g0)')"H 12 1 " // adjustl(trim(units))
        write(64,'(a3)')"N 1"
        do i=1,nConLev
          write(64,'(g0)')adjustl(trim(legpenstr(i)))
        enddo
      close(63)

      if(IsLatLon)then
        xmin = minval(lon_cc_pd(1:nx))
        ! Make sure xmin is in the range -180->180
        if (xmin.gt.180.0_ip)then
          xmin = minval(lon_cc_pd(1:nx))-360.0_ip
          xmax = maxval(lon_cc_pd(1:nx))-360.0_ip
        else
          xmax = maxval(lon_cc_pd(1:nx))
        endif
        ymin = minval(lat_cc_pd(1:ny))
        ymax = maxval(lat_cc_pd(1:ny))
      else
        xmin = minval(x_cc_pd(1:nx))
        xmax = maxval(x_cc_pd(1:nx))
        ymin = minval(y_cc_pd(1:ny))
        ymax = maxval(y_cc_pd(1:ny))
      endif
      call citylist(2,xmin,xmax,ymin,ymax, &
                      ncities,                        &
                      lon_cities,lat_cities,          &
                      name_cities)

      if(lon_volcano.gt.xmax)lon_volcano=lon_volcano-360.0_ip

      ! Start writing the gmt bits
      ! BASE string:
      if(xmax-xmin.le.2.0_ip)then
        write(base_str,*)"-Ba0.25"
        write(detail_str,*)"-Dh"
      elseif(xmax-xmin.le.5.0_ip)then
        write(base_str,*)"-Ba1"
        write(detail_str,*)"-Dh"
      elseif(xmax-xmin.le.10.0_ip)then
        write(base_str,*)"-Ba2"
        write(detail_str,*)"-Dh"
      elseif(xmax-xmin.le.20.0_ip)then
        write(base_str,*)"-Ba5"
        write(detail_str,*)"-Dh"
      elseif(xmax-xmin.le.40.0_ip)then
        write(base_str,*)"-Ba10"
        write(detail_str,*)"-Dl"
      else
        write(base_str,*)"-Ba20"
        write(detail_str,*)"-Dl"
      endif
      write(title_str,*)'-B+t"',trim(adjustl(title_plot)),units,'"'

      ! AREA string:    AREA="-R$lonmin/$lonmax/$latmin/$latmax"
      area_str = " -R"
      write(flt_str,'(f8.3)')xmin
      area_str = trim(area_str) // adjustl(trim(flt_str))
      write(flt_str,'(f8.3)')xmax
      area_str = trim(area_str) // "/" // adjustl(trim(flt_str))
      write(flt_str,'(f8.3)')ymin
      area_str = trim(area_str) // "/" // adjustl(trim(flt_str))
      write(flt_str,'(f8.3)')ymax
      area_str = trim(area_str) // "/" // adjustl(trim(flt_str))

      ! PROJ string:    PROJ="-JM${VCLON}/${VCLAT}/20"
      proj_str = " -JM"
      write(flt_str,'(f8.3)')lon_volcano
      proj_str = trim(proj_str) // adjustl(trim(flt_str))
      write(flt_str,'(f8.3)')lat_volcano
      proj_str = trim(proj_str) // "/" // adjustl(trim(flt_str)) 
      proj_str = trim(proj_str) // "/7i"

      ! COAST string
      write(coast_str,*)" -G220/220/220 -W"

      ! RIVER string
      !if()then ! small domain, include rivers
      !  write(river_str,*)" -I1/1p,blue -I2/0.25p,blue"
      !else
        write(river_str,*)" "
      !endif

      ! Initial pscoast command to start the ps file
      start_ps = "-K > "     // adjustl(trim(fileps))
      contn_ps = "-K -O >> " // adjustl(trim(fileps))
      end_ps   = "-O >> "    // adjustl(trim(fileps))

      ! Set up to plot via GMT script
      write(55,*)'#!/bin/bash'
      if(.not.writeContours)write(55,*)'gmt gmtset PROJ_ELLIPSOID Sphere'
      cmd = 'gmt psbasemap -X1.5i -Y2i ' // adjustl(trim(area_str))   // " " // &
                              adjustl(trim(proj_str))   // " " // &
                              adjustl(trim(base_str))   // " " // &
                              adjustl(trim(title_str))  // " " // &
                              adjustl(trim(start_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      cmd = "gmt pscoast " // adjustl(trim(area_str))   // " " // &
                              adjustl(trim(proj_str))   // " " // &
                              adjustl(trim(detail_str)) // " " // &
                              adjustl(trim(coast_str))  // " " // &
                              adjustl(trim(river_str))  // " " // &
                              adjustl(trim(contn_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      ! Get grid to contour, converting the ASCII file generated above
      ! We need this line regardless of if we are just making contours or not
      cmd = "gmt grdconvert outvar.dat=ef out.grd"
      write(55,*)adjustl(trim(cmd))

      ! write contour
      !gmt grdcontour out.grd $AREA $PROJ $BASE -Cdpm_1.lev    -A- -W3,0/128/255   -O -K >> temp.ps
      do ilev=1,nConLev
        write(55,*)'echo "',real(ContourLev(ilev),kind=4), '   C" > c.lev'
        cmd = "gmt grdcontour out.grd " // adjustl(trim(area_str))  // " " // &
                              adjustl(trim(proj_str))  // " " // &
                              "-Cc.lev -A- " // adjustl(trim(penstr(ilev))) // " " // &
                              adjustl(trim(contn_ps))
        if(.not.writeContours)write(55,*)adjustl(trim(cmd))

        if(writeContours)then
          ! write contours to files
          write(dp_confile,54) "out",ilev,".con"
 54       format(a3,i0.2,a4)  ! write contour file name with level zero-padded
          !gmt grdcontour out.grd $AREA $PROJ $BASE -Cc.lev    -A- -W3,0/128/255 -Dcfile.xyz
!          write(55,*)'echo "',real(ContourLev(i),kind=4), '   C" > c.lev'
          cmd = "gmt grdcontour out.grd " // adjustl(trim(area_str))  // " " // &
                                adjustl(trim(proj_str))  // " " // &
                                "-Cc.lev -A- " // adjustl(trim(penstr(ilev))) // " -D" // adjustl(trim(dp_confile))
          write(55,*)adjustl(trim(cmd))
        endif
      enddo

      ! Add cities.
      do i=1,ncities
        write(dumstr17,'(f8.3,1x,f8.3)')lon_cities(i),lat_cities(i)
        cmd = "echo " // dumstr17 // " '1.0' | gmt psxy " &
              // adjustl(trim(area_str))  // " " // &
              adjustl(trim(proj_str))  // " " // &
              "-Sc0.05i -Gblack -Wthinnest " // adjustl(trim(contn_ps))
        if(.not.writeContours)write(55,*)adjustl(trim(cmd))

        write(dumstr48,'(a1,f8.3,1x,f8.3,1x,a1,a26,a1,a1)')'"',&
               lon_cities(i),lat_cities(i),"'",adjustl(trim(name_cities(i))),"'",'"'
        cmd = "echo " // adjustl(trim(dumstr48)) // " | gmt pstext " &
              // adjustl(trim(area_str))  // " " // &
              adjustl(trim(proj_str))  // " " // &
              "-D0.1/0.1 -V " // adjustl(trim(contn_ps))
        if(.not.writeContours)write(55,*)adjustl(trim(cmd))
      enddo

      ! Last gmt command is to plot the volcano and close out the ps file
      ! echo $VCLON $VCLAT '1.0' | ${GMTpre[GMTv]} psxy $AREA $PROJ -St0.1i -Gblack -Wthinnest -O >> temp.ps
      write(dumstr17,'(f8.3,1x,f8.3)')lon_volcano,lat_volcano
      cmd = "echo " // dumstr17 // " '1.0' | gmt psxy " &
            // adjustl(trim(area_str))  // " " // &
            adjustl(trim(proj_str))  // " " // &
            "-St0.1i -Gmagenta -Wthinnest " // adjustl(trim(contn_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      ! Add descriptive footer here
      !gmt pslegend leg1.txt -R-134.500/-97.500/33.500/52.500  -JM-122.117/42.933/7i -Dx-1.0i/-1.5i/3.0i/1.0i/BL -K -O  >> temp.ps
      cmd = "gmt pslegend leg1.txt " // adjustl(trim(area_str)) &
                                     // adjustl(trim(proj_str)) &
                                     // " -Dx-1.0i/-1.5i/3.0i/1.0i/BL " &
                                     // adjustl(trim(contn_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      !gmt pslegend leg2.txt -R-134.500/-97.500/33.500/52.500  -JM-122.117/42.933/7i -Dx2.0i/-1.5i/4.0i/1.0i/BL -K -O  >> temp.ps
      cmd = "gmt pslegend leg2.txt " // adjustl(trim(area_str)) &
                                     // adjustl(trim(proj_str)) &
                                     // " -Dx2.0i/-1.5i/4.0i/1.0i/BL " &
                                     // adjustl(trim(contn_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      !gmt pslegend leg3.txt -R-134.500/-97.500/33.500/52.500  -JM-122.117/42.933/7i -Dx6.0i/-2.3i/2.0i/1.0i/BL -K -O  >> temp.ps
      inquire( file="leg3.txt", exist=IsThere)
      if(IsThere)then
        cmd = "gmt pslegend leg3.txt " // adjustl(trim(area_str)) &
                                     // adjustl(trim(proj_str)) &
                                     // " -Dx6.0i/-2.3i/2.0i/1.0i/BL " &
                                     // adjustl(trim(contn_ps))
        if(.not.writeContours)write(55,*)adjustl(trim(cmd))
      endif
      ! Add contour legend here
      cmd = "gmt pslegend leg4.txt " // adjustl(trim(area_str)) &
                                     // adjustl(trim(proj_str)) &
                                     // " -Dx8.0i/2.0i/1.25i/2.5i/BL -F " &
                                     // adjustl(trim(end_ps))
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      !gmt pslegend leg4.txt -R-134.500/-97.500/33.500/52.500  -JM-122.117/42.933/7i -Dx8.0i/2.0i/1.25i/2.5i/BL -F -O  >> temp.ps

      ! This command converts temp.ps to temp.png
      cmd = "gmt psconvert temp.ps -A -Tg"
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      ! Might need to add this check in the GMT script on version 5 vs 6 (5 needs rotation)
      !GMTv=`gmt --version | cut -c1`
      !if [ $GMTv -eq 5 ] ; then
      !  convert -rotate 90 temp.png -resize 630x500 -alpha off temp.gif
      !if [ $GMTv -eq 5 ] ; then
      !  convert temp.png -resize 630x500 -alpha off temp.gif

      ! Move this png to the final filename
      cmd = "mv temp.png " // outfile_name
      if(.not.writeContours)write(55,*)adjustl(trim(cmd))

      ! Clean up
      cmd = "rm -f c.lev out.grd temp.* leg*.txt out*.con cities.xy"
      write(55,*)adjustl(trim(cmd))

      close(55)
      write(gmtcom,'(a3,a14)')'sh ',dp_gmtfile
      call execute_command_line(gmtcom,exitstat=istat)

      ! GMT script has been run, now we need to read the contour lines if we want
      ! to write a shapefile
      if(writeContours)then
        ContourDataNcurves(:) = 0
        do ilev=1,nConLev
          write(dp_confile,54) "out",ilev,".con"
          ! Read dp_confile
          do io=1,2;if(VB(io).le.verbosity_info)then
            write(outlog(io),*)"Now trying to read contour file and loading contour data.",&
                adjustl(trim(dp_confile))
          endif;enddo
          inquire( file=trim(adjustl(dp_confile)), exist=IsThere)
          if(IsThere)then
            open(54,file=dp_confile,status='old')
          else
            ! File does not exist; try next contour level
            cycle
          endif
        ! In the GMT contour files, all contours of a have a header in the following
        ! format:
        !> 0.01 contour -Z0.01
        ! Each curve for that level is separated by a line containing '>'

        read(54,'(a80)',iostat=ioerr)linebuffer080
        do while(ioerr.ge.0)
          ! Check if this is a header line
          read(linebuffer080,*,iostat=ioerr2)testkey
          if(ioerr2.lt.0)then
            ! If reading one character fails, then there is a blank line
            ! shouldn't happen with  GMT v 5 or 6
            ! Log zero contours for this level
            ioerr = -1
            ContourDataNcurves(ilev) = 0
            cycle

          elseif(testkey.eq.'>')then
            ! This is a header line
            !   n it will have this format:> 0.03 contour -Z0.03
            substr_pos1 = index(linebuffer080,'contour')

            if(substr_pos1.eq.0)then
              ! key > is present, but 'contour' is not
              read(54,'(a80)',iostat=ioerr)linebuffer080
              cycle
            else
              ! Contours are all written to separate files
              ! Increment the number of curves for this level
              ContourDataNcurves(ilev) = ContourDataNcurves(ilev) + 1
              icurve = ContourDataNcurves(ilev)
            endif
          else
            ! This is the data section
            ! Increment the number of points
            ContourDataNpoints(ilev,icurve) = ContourDataNpoints(ilev,icurve) + 1
            ipt = ContourDataNpoints(ilev,icurve)
            read(linebuffer080,*)ContourDataX(ilev,icurve,ipt),ContourDataY(ilev,icurve,ipt)
          endif

            ! Try to read the next line
            read(54,'(a80)',iostat=ioerr)linebuffer080
          enddo    ! ioerr.ge.0
          close(54)  ! Close this contour file and open the next
        enddo    ! ilev=1,nConLev

        ! Loop through all the levels and curves and trim any curves with zero length
        do ilev=1,nConLev
          ! start at the last contour level
          icurve = Contour_MaxCurves + 1
          do ii=Contour_MaxCurves,1,-1
            if(ContourDataNpoints(ilev,ii).le.0)then
              ! log each curve number with no points
              icurve = ii
            endif
          enddo
          ContourDataNcurves(i) = max(0,icurve-1)
        enddo
      endif





      ! This is a test section that uses the GMT API
      !character(16) filedat,fileps
      !character(200) cmd
      !
      !filedat="ex01-xy.dat"
      !fileps="ex01.ps"
      !
      !! create GMT session
      !call sGMT_Create_Session("Test")
      !
      !! call psxy
      !cmd="psxy "//trim(filedat)//" -JX16c/24c -R0/11/0/110 -Bx2+lx -By20+ly -BWS+tFig. -Sa1c -N -P ->"//trim(fileps)
      !call sGMT(cmd)
      !print *,trim(cmd)
      !
      !! call ps2raster
      !cmd="ps2raster "//trim(fileps)//" -Tg"
      !call sGMT(cmd)
      !print *,trim(cmd)
      !
      !! clean up
      !call sGMT_Destroy_Session()

      end subroutine write_2Dmap_PNG_GMT


!     All subroutines below are placeholder copies of the gnuplot subroutines.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_2Dprof_PNG_GMT
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    vprof_ID        = ID of the profile (number in list from Ash3d control file)
!
!  This subroutine creates a png plot of the transient vertical profile of ash
!  concentration above the profile point (vprof_ID) using the GMT graphics
!  package.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_2Dprof_PNG_GMT(vprof_ID)

      use precis_param

      use mesh,          only : &
         nzmax,z_cc_pd

      use Output_Vars,   only : &
         pr_ash

      use io_data,       only : &
         Site_vprofile,x_vprofile,y_vprofile,cdf_b3l1,VolcanoName

      use Source,        only : &
         neruptions,e_Volume,e_Duration,e_StartTime,e_PlumeHeight

      use time_data,     only : &
         os_time_log,BaseYear,useLeap,ntmax,time_native

      integer, intent (in) :: vprof_ID

      do io=1,2;if(VB(io).le.verbosity_error)then
        write(errlog(io),*)"ERROR: write_2Dprof_PNG_GMT is not yet implemented."
        write(errlog(io),*)"       Please choose a different plotting package for profiles"
      endif;enddo
      stop 1

      end subroutine write_2Dprof_PNG_GMT

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!  write_DepPOI_TS_PNG_GMT
!
!  Called from: Ash3d_PostProc.f90
!  Arguments:
!    pt_indx       = index of point in Ash3d netcdf output file
!
!  This subroutine creates a png plot of the transient deposit accumulation at
!  the airport/POI given by pt_index using the GMT graphics package.
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine write_DepPOI_TS_PNG_GMT(pt_indx)

      use precis_param

      use Output_Vars,   only : &
         THICKNESS_THRESH

      use Airports,      only : &
         Airport_Name,Airport_Thickness_TS

      use io_data,       only : &
         nWriteTimes,WriteTimes

      use time_data,     only : &
         Simtime_in_hours

      integer,intent(in) :: pt_indx

      do io=1,2;if(VB(io).le.verbosity_error)then
        write(errlog(io),*)"ERROR: write_DepPOI_TS_PNG_GMT is not yet implemented."
        write(errlog(io),*)"       Please choose a different plotting package for POI dep."
      endif;enddo
      stop 1

      end subroutine write_DepPOI_TS_PNG_GMT

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      end module Ash3d_PostProc_GMT

!##############################################################################
